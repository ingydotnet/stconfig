#!/usr/bin/perl
use warnings;
use strict;

use Getopt::Long;
use Socialtext::Resting::Getopt qw(get_rester rester_usage);
use YAML;

# It would be nice if ST::Resting::Getopt let us specify defaults for some of
# these things.
unshift @ARGV, '--server=https://www.socialtext.net/', '--workspace=buildbot_config';
my $rester = get_rester();

sub usage { print <<END_USAGE; exit 0 }
USAGE: bbcfg ARGS

This script changes the buildbot settings stored in the wiki at
https://www.socialtext.net/lite/page/buildbot_config/.

   branches show
   branches add BRANCH    (e.g., 'branches/interwiki-search',
   branches remove BRANCH        'privates-branches/wired')

   slaves show
   slaves add SLAVENAME PASSWORD
   slaves password SLAVENAME NEWPASS
   slaves remove SLAVENAME

   silly show
   silly set QUERY RESPONSE
   silly clear QUERY

Keywords can be abbreviated as long as they remain unique.  E.g.,

  bbcfg br sh

END_USAGE

my %action = (
    branches => {
        show   => sub { yaml_show_list('branches') },
        add    => sub { yaml_append( 'branches', shift ) },
        remove => sub { yaml_remove( 'branches' ), shift },
    },
    slaves => {
        show   => \&show_slaves,
        add    => sub { yaml_append( 'slaves',
                { 'slave-name' => shift, 'password' => shift } ) },
        remove => sub { remove_slave( shift ) },
    },
    silly => {
        show  => sub { yaml_show_dict('silly') },
        set   => sub { yaml_set( 'silly', shift, shift ) },
        clear => sub { yaml_clear( 'silly', shift ) },
    },
    help => \&usage,
);

usage() unless @ARGV;

SUBJECT: for my $subject (keys %action) {
    next SUBJECT unless starts_with( $subject, $ARGV[0] );
    shift;

    if (ref $action{$subject} eq 'CODE') {
        $action{$subject}->(@ARGV);
        exit;
    } else {
        VERB: for my $verb ( keys %{ $action{$subject} } ) {
            next VERB unless starts_with( $verb, $ARGV[0] );
            shift;
            $action{$subject}{$verb}->(@ARGV);
            exit;
        }
    }
}

usage();

# The slave data is arranged sort of weirdly.
#
sub show_slaves {
    printf "%30.30s %-30.30s\n", 'USERNAME', 'PASSWORD';
    for my $slave_spec (@{ yaml_get('slaves') }) {
        printf "%30.30s %-30.30s\n", $slave_spec->{'slave-name'}, $slave_spec->{password};
    }
}

sub remove_slave {
    my ( $slave_name ) = @_;

    my $slaves = [ grep $_->[0] ne $slave_name, @{ yaml_get('slaves') } ];
    yaml_put( 'slaves', $slaves );
}

# Lists
#
sub yaml_show_list {
    my ( $page_id ) = @_;

    print "$_\n" for @{ yaml_get($page_id) };
}

sub yaml_append {
    my ( $page_id, $item ) = @_;

    yaml_put( $page_id, [ @{ yaml_get($page_id) }, $item ] )
}

sub yaml_remove {
    my ( $page_id, $item ) = @_;

    yaml_put(
        $page_id,
        [ grep $_ ne $item, @{ yaml_get($page_id) } ] );
}

# Dicts
#
sub yaml_show_dict {
    my ( $page_id ) = @_;

    my $dictref = yaml_get($page_id);

    while ( my ( $k, $v ) = each %$dictref ) {
        printf "$k\n  $v\n";
    }
}

sub yaml_set {
    my ( $page_id, $key, $value ) = @_;

    my $dictref = { %{ yaml_get($page_id) }, $key => $value };
    yaml_put( $page_id, $dictref );
}

sub yaml_clear {
    my ( $page_id, $key ) = @_;

    my $dictref = yaml_get($page_id);
    delete $dictref->{$key};
    yaml_put( $page_id, $dictref );
}

# Getting and putting YAML pages.
#
sub yaml_put {
    my ( $page_id, $content ) = @_;

    $rester->put_page( $page_id, ".pre\n" . Dump($content) . ".pre\n" );
}

sub yaml_get {
    my ( $page_id ) = @_;

    my $content = $rester->get_page($page_id);
    $content =~ s/(?:\A\.pre\n|^\.pre\n?\Z)//mg;
    return Load($content);
}

# Handy little function
#
sub starts_with {
    my ( $long, $short ) = @_;

    return (substr $long, 0, length $short) eq $short;
}
