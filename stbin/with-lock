#!/usr/local/bin/perl
use warnings;
use strict;

use Fcntl ':flock';

sub usage {
    print <<'END_OF_HELP';
    USAGE:
        with-lock LOCKFILE COMMAND [ARGS ...]

        Tries to acquire an exclusive lock on LOCKFILE.  Exits nonzero
        immediately if LOCKFILE does not exist.  Exits 0 immediately if it is
        unable to acquire the lock.  Otherwise, execs COMMAND with ARGS (the
        lock will continue to be held after the exec).  When COMMAND exits,
        the lock will automatically be released.
END_OF_HELP
    exit 0;
}

my $lockfile = shift;
usage() unless @ARGV;

open L, $lockfile or die "$lockfile: $!";
flock L, LOCK_EX|LOCK_NB or exit 0;

# The next line works because flock(2) locks are preserved across execve(2).
exec @ARGV;
die "exec $ARGV[0]: $!";
