#!/usr/bin/perl
use strict;
use warnings;
use lib "$ENV{HOME}/src/st/current/nlw/lib";
use Socialtext::System qw/shell_run/;
use Getopt::Long;
use Socialtext::BuildUtils qw/display/;

my %opts;
GetOptions( \%opts,
    'pretend',
    'message=s',
    'from=s',
    'repo=s',
    'force',
) or usage();

my $branch = shift;
usage() unless $branch;

my $from_branch = $opts{from} ? "branches/$opts{from}" : "trunk";
my $smerge_opts  = $opts{pretend} ? '-C ' : '';

my %repo_names = map { $_ => 1 } split /\s+/, qx(st-repo-list);
$opts{repo} ||= 'all';
$opts{repo} = join(',', keys %repo_names) if $opts{repo} =~ m/^all$/i;
my @repos = split ',', $opts{repo};
for my $r (@repos) {
    usage("$r is not a valid repository") unless $repo_names{$r};
}

$opts{message} ||= "update-branch $branch from $from_branch";
$smerge_opts .= "-m '$opts{message}'";

cowsay("Updating $branch from $from_branch");
for my $repo (@repos) {
    my $svk_base = "//mirror/$repo";
    my $branch_prefix = "branches/";
    $branch_prefix = '' if $branch =~ m#tags/#;
    my $branch_path = "$svk_base/$branch_prefix$branch";
    my $from_dir = $opts{from} || 'trunk';

    (my $r_branch = $branch) =~ s#tags/##;
    if ($repo ne 'socialtext') {
        $from_dir = "$repo/$from_branch";
        $r_branch = "$repo/$branch_prefix$branch";
    }

    cowsay("Updating $repo repository.", 'magenta');
    eval {
        map { shell_run($_) }
            "svk up -s ~/src/st/$from_dir",
            "svk up -s ~/src/st/$r_branch",
            "svk smerge $smerge_opts $svk_base/$from_branch $branch_path",
            "svk up -s ~/src/st/$r_branch",
            ;
    };
    if ($@) {
        warn "Error: $@\n";
        exit unless $opts{force};
    }
    else {
        cowsay("Branch $branch updated!", 'green');
    }
}
cowsay("All done!");

exit;

sub usage {
    die <<EOT;
$0 [rester options] [options] new-branch-name

Update a branch from a base branch using SVK.

Requires you have SVK set up!

options:
  --pretend     don't really do the merge (svk smerge -C)
  --from        branch to update from (default: trunk)
  --repo <repo> Specify the repo to use
  --force       Don't ... give ... up ...
EOT
}

sub cowsay {
    my $msg = shift;
    my $colour = shift || 'green';
    my $cowsay = qx(cowsay '$msg');
    display($cowsay, $colour);
}
