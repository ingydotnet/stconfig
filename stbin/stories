#!/usr/bin/perl
use strict;
use warnings;
use Socialtext::Resting::Getopt qw/get_rester/;
use Socialtext::WikiObject;
use Socialtext::BuildUtils qw/display/;
use Getopt::Long;

my $r = get_rester(
    server => 'https://www2.socialtext.net',
    workspace => 'dev-tasks',
);

my %opts;
GetOptions( \%opts,
    'c|completed',
) or usage();

my $page_name = shift;

unless ($page_name) {
    $r->accept('text/plain');
    my @iterations = $r->get_taggedpages('current iteration');
    $page_name = shift @iterations || die "No iterations found!";
}
$r->accept('text/x.socialtext-wiki');
my $page = Socialtext::WikiObject->new(
    rester => $r,
    page => $page_name,
);

delete $page->{rester};
my $stories = $page->{stories};

if (ref($stories->{text}) eq 'HASH') {
    my $not_started = $stories->{text}{table};
    if ($not_started and @$not_started) {
        my ($headings, @stories) = @$not_started;
        unshift @stories, $headings unless $headings->[0] =~ m/Story/;
        display("Stories Not Started", 'yellow');
        print_stories(\@stories);
    }
}

# Figure out what the headings are
for my $k (keys %$stories) {
    delete $stories->{$k} if $k eq lc($k);
}

for my $category (keys %$stories) {
    next if !$opts{completed} and $category eq 'Completed';
    my $section = $stories->{$category};
    my $table = ref($section) eq 'ARRAY' ? $section : $section->{table};
    unless (ref($table) eq 'ARRAY') {
        warn "Unknown category: $category - " . ref($table);
        next;
    }
    my ($headings, @stories) = @$table;
    if (@stories) {
        display($category, 'green');
        print_stories(\@stories);
    }
}
exit;


sub print_stories {
    my $stories = shift;
    for my $s (@$stories) {
        display("  " . join(', ', grep { $_ } $s->[0], $s->[3], $s->[4], $s->[5], $s->[7]), 'cyan');
    }
}

sub usage {
    die <<EOT
USAGE: $0 [options] [Iteration Page Name]
Where options are:

  -c --completed Show completed stories too (normally they're hidden)
EOT
}
