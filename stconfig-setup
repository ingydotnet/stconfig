#!/usr/bin/perl

use strict;
use warnings;
use FindBin;
use Cwd;

{
    my $base = $FindBin::Bin;
    $base = Cwd::abs_path($base);

    die unless $ENV{HOME};
    chdir($ENV{HOME});

    make_link($base, $_) for all_files($base);
}

# XXX glob this or such
sub all_files {
    my $base = shift;
    opendir DIR, $base or die;
    my @files;
    while (my $file = readdir(DIR)) {
        next if $file =~ /^(\.|\.\.|\.svn|stconfig-setup|socialtext\.vim)$/;
        push @files, $file;
    }
    return sort @files;
}

sub make_link {
    my $base = shift;
    my $file = shift;
    my $dest = "$base/$file";
    if (-e $file) {
        warn "    skipping $file, already exists.\n"
            # no need to warn if it's a known symlink
            if not(-l $file) or $dest ne readlink($file);
        return;
    }
    warn "Linking $file\n";
    symlink($dest, "$ENV{HOME}/$file");
}
