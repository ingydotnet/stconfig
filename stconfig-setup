#!/usr/local/bin/perl

use strict;
use warnings;
use FindBin;
use Cwd;
use File::chdir;

{
    my $base = $FindBin::Bin;
    $base = Cwd::abs_path($base);

    die unless $ENV{HOME};
    chdir($ENV{HOME});

    link_or_descend($base, $_) for all_files($base);
}

sub all_files {
    my $base = shift;
    my %ignores = map { $_ => 1 } qw(
        .
        ..
        .svn
        stconfig-setup
        socialtext.vim
    );
    opendir DIR, $base or die;
    my @files;
    while (my $file = readdir(DIR)) {
        push @files, $file
          unless $ignores{$file};
    }
    return sort @files;
}

sub link_or_descend {
    my $base = shift;
    my $file = shift;
    my $dest = "$base/$file";
    if (-d $file && ! -l $file && -d $dest) {
        warn "Descending into directory $file\n";
        {
            local $CWD = $file;
            link_or_descend($dest, $_) for all_files($dest);
        }
    } elsif (-e $file) {
        warn "    skipping $file, already exists.\n"
            # no need to warn if it's a known symlink
            if not(-l $file) or $dest ne readlink($file);
    } else {
        warn "Linking $file\n";
        symlink($dest, $file);
    }
}
